set shell := ["bash", "-c"]

# List available tasks
default:
	@just --list

# Create virtual environment with uv
venv:
	uv venv

# Install dependencies from uv.lock
sync:
	uv sync --all-groups

# Run the main entrypoint
run:
	uv run python main.py

# Run tests including UI
test-all:
	uv run pytest

# Run the UI server
ui-dev:
	uv run python -m ui.main

# Run UI tests
ui-test:
	PYTHONPATH=src uv run pytest tests/ui/

# Run Ruff lint checks
lint:
	uv run ruff check .

# Auto-fix Ruff lint issues
fix:
	uv run ruff check . --fix

# View the DB using Datasette
view-db:
    uvx datasette data/tracker.sqlite

# Record dataset interactively into DB
# Usage: just record-dataset [dataset_name="real_voice_train"]
record-dataset dataset_name="real_voice_train":
	uv run python -m lora_data.recorder --dataset-name {{dataset_name}}

# Review audio interactively
# Usage: just review-audio [dir="data/synthetic_audio"]
review-audio dir="data/synthetic_audio":
	uv run python -m lora_data.player --dir {{dir}}

# Split the dataset into train and eval in the database
# Usage: just split-dataset <dataset_name> [test_ratio="0.1"]
split-dataset dataset_name test_ratio="0.1":
	uv run python -m lora_data._dataset_splitter --dataset-name {{dataset_name}} --test-ratio {{test_ratio}}

# Transcribe a single audio file or a batch manifest
# Usage: just transcribe "--model-id <id> --audio <path>"
transcribe args:
	uv run python -m lora_training.transcribe {{args}}

# Run interactive STT recorder to capture corrections for hard-negative mining
# Usage: just correction-recorder [dataset_name="corrections_manifest"]
correction-recorder dataset_name="corrections_manifest":
	uv run python -m lora_data.correction_recorder --dataset-name {{dataset_name}}

# Run an experiment in the background using nohup
# Usage: just run-experiment <name> "<extra_args>"
# Example: just run-experiment exp_01 "--max-steps 1000 --learning-rate 1e-5"
run-experiment name args="":
	@echo "Creating output directory outputs/{{name}}..."
	@mkdir -p "outputs/{{name}}"
	@echo "Starting experiment {{name}} in the background..."
	nohup uv run python main.py --output-dir "outputs/{{name}}" {{args}} > "outputs/{{name}}/nohup.out" 2>&1 &
	@echo "Experiment started! Logs will be written to outputs/{{name}}/experiment.log"
	@echo "To poll the formatted logs, run: just poll {{name}} <timeout>"
	@echo "To poll the raw terminal output, run: just poll-raw {{name}} <timeout>"

# Poll the formatted experiment.log for a running experiment for a given timeout (seconds)
# Usage: just poll <name> [timeout=60]
poll name timeout="60":
	@echo "Polling logs for {{timeout}} seconds..."
	@python3 -c 'import subprocess, time; p = subprocess.Popen(["tail", "-f", "outputs/{{name}}/experiment.log"]); time.sleep({{timeout}}); p.terminate()'

# Poll the raw stdout/stderr (nohup.out) for an experiment for a given timeout (seconds)
# Usage: just poll-raw <name> [timeout=60]
poll-raw name timeout="60":
	@echo "Polling raw logs for {{timeout}} seconds..."
	@python3 -c 'import subprocess, time; p = subprocess.Popen(["tail", "-f", "outputs/{{name}}/nohup.out"]); time.sleep({{timeout}}); p.terminate()'

# Check if an experiment process is still running
# Usage: just status <name>
status name:
	@ps aux | grep "outputs/{{name}}" | grep -v "grep" || echo "No running process found for {{name}}."
